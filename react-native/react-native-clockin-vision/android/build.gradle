buildscript {
    ext {
        kotlin_version = '1.3.72'
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                cFlags    "-O3"
                cppFlags  "-std=c++11 -frtti -fexceptions"
                arguments "-DANDROID_PLATFORM=android-22",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_static",
                        "-DANDROID_CPP_FEATURES=rtti exceptions"
            }
        }
        sourceSets {
            main {
                jniLibs.srcDirs = [
                        "src/main/cppLibs/dlib/lib",
                        "src/main/cppLibs/opencv/lib"
                ]
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    /*
     *  ============= CAUTIOUSLY READ AND CHANGE THIS =============
     *
     * Please keep in mind that the runtime react-native library used to compile
     * the library is different from the version resolved by gradle when used in a react-native
     * project. The version resolved by gradle which is around 0.20.1 while the one resolved
     * when compiling the library included in react native project depends on the version
     * used when creating the project.
     *
     * It turns out that the 0.20.1 library is not compatible with the runtime found
     * in node_modules. That said, DON'T fix the compile time errors on this file. They're there
     * to reflect the actual signature of the runtime library with which the project is
     * compiled.
     *
     * In order to allow the static errors disappear while working on this library, you can
     * specify from where to pull the react-native library implementation by setting to true
     * the flag [OVERRIDE_REACT_NATIVE] and setting a home directory for the node_modules.
     *
     * The impact that this will have on a project if the flag remain enabled, was not tested,
     * that said, whenever you finish working on this library, please switch the flag to false
     * back again.
     *
     * @see also [ReactCameraModule]
     */
    def OVERRIDE_REACT_NATIVE = false
    def NODE_MODULES_HOME     = "$rootDir/react-native-totvs-camera-app/node_modules"

    if (OVERRIDE_REACT_NATIVE) {
        maven {
            url "$NODE_MODULES_HOME/react-native/android"
        }
    }

    mavenCentral()
    google()
    jcenter()
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.4"

    implementation project(":totvs-camera-core")
    implementation project(":totvs-camera-view")
    implementation project(":totvs-camera-vision")

    // React Native
    implementation 'com.facebook.react:react-native:+' // from node_modules
}
  